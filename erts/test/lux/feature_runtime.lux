[doc Test general working of runtime state of features]

[include features.luxinc]

[invoke setup]

[newshell erl]
    !export OTP_TEST_FEATURES=true
    ?SH-PROMPT:
    -undefined function
    !erl
    ?$ERLPROMPT

    [invoke same-members "erl_features:enabled_features()" "[cond_expr,maps]"]

    !erl_features:keywords().
    ??[]
    ?$ERLPROMPT

    [invoke same-members "erl_features:enable_feature(while_expr)" "[cond_expr,maps,while_expr]"]

    !erl_features:keywords().
    ??['while','until']
    ?$ERLPROMPT

    [invoke same-members "erl_features:disable_feature(while_expr)" "[cond_expr,maps]"]

    !erl_features:keywords().
    ??[]
    ?$ERLPROMPT

    [invoke same-members "erl_features:enable_feature(ifn_expr)" "[cond_expr,maps,ifn_expr]"]

    [invoke same-members "erl_features:enable_feature(while_expr)" "[cond_expr,maps,ifn_expr,while_expr]"]

    [invoke same-members "erl_features:enabled_features()" "[cond_expr,maps,ifn_expr,while_expr]"]

    !erl_features:keywords().
    ??['while','until','ifn']

    [invoke same-members "erl_features:disable_feature(ifn_expr)" "[cond_expr,maps,while_expr]"]

    [invoke same-members "erl_features:enabled_features()" "[cond_expr,maps,while_expr]"]

    !erl_features:keywords().
    ??['while','until']
    ?$ERLPROMPT

    [invoke same-members "erl_features:disable_feature(while_expr)" "[cond_expr,maps]"]

    !erl_features:keywords().
    ??[]
    ?$ERLPROMPT

    !erl_features:enable_feature(misfeature).
    ??invalid_feature
    ??argument 1: unknown feature
    ?$ERLPROMPT

[shell erl]
    [invoke quit-erl]

    [invoke start-erl ""]
    !while.
    ?while
    ?$ERLPROMPT

    # If there is no Meta chunk, we should just get [] as answer,
    # not a failure.  We use a file compiled with an older release to be
    # sure no meta chunk is present
    !erl_features:features_used(older).
    ???[]
    ?$ERLPROMPT

    !erl_features:features_used("older.beam").
    ???[]
    ?$ERLPROMPT


    # Test getting used features for unknown modules and files
    !erl_features:features_used(no_module).
    ??not_found
    ?$ERLPROMPT

    !erl_features:features_used("none.beam").
    ??not_found
    ?$ERLPROMPT

[cleanup]
    [invoke no-dump]
